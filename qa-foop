#!/usr/bin/newlisp

(define (complex:complex (r 0) (i 0)) 
	(list complex r i)) 

; from Michael 10-25-2007 in Forum/News

(define (complex:rad c) 
   (set 're (c 1) 'im (c 2)) 
   (sqrt (add (mul re re) (mul im im))) 
) 

(define (complex:theta c) 
	(atan (div (c 1) (c 2)))) 

(define (complex:add a b) 
	(complex (add (a 1) (b 1)) (add (a 2) (b 2)))) 

(define (complex:mul a b) 
   (set 
      'a.re (a 1) 'a.im (a 2) 
      'b.re (b 1) 'b.im (b 2) 
   ) 
   (complex 
      (sub (mul a.re b.re) (mul a.im b.im)) 
      (add (mul a.re b.im) (mul a.im b.re)) 
   ) 
)
 
(define (complex:square c) 
	(:mul c c))


;;;;;;;;; from Cormullion 10-25-2007 in Forum/News

(define (draw) 
  (for (y -1 1.1 0.07) 
      (for (x -2 1 0.03) 
         (set 'z (complex x y) 'c 126 'a z ) 
         (while (and (< (abs (:rad (set 'z (:add (:mul z z) a)))) 2) (> (dec c) 32)) 
           ) 
         (print (char c)) 
      ) 
  (println))) 

(draw) 

(exit)

;; eof
